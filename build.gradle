/*
 * This is a general purpose Gradle build.
 * Learn how to create Gradle builds at https://guides.gradle.org/creating-new-gradle-builds/
 */

plugins {
  id 'java'
  id 'war'
  id 'org.akhikhl.gretty' version '1.4.2'
  id "com.github.hierynomus.jython" version "0.8.0"
}


dependencies {
  compile 'org.python:jython-standalone:2.7.1'

  // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
  compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.21.0.1'

  testJython ":nose:1.3.7"

  jython python(":SQLAlchemy:0.7.2") {
    moduleName = "sqlalchemy"
    copy {
      // within SQLAlchemy-0.7.2.tar.gz
      from "lib/sqlalchemy"
    }
  }

  jython python(":JayDeBeApi:1.1.1") {
    moduleName = "jaydebeapi"
    copy {
      from "jaydebeapi"
    }
  }
}

repositories {
    mavenCentral()
}

task noseTests(type: JavaExec) {
  group = 'verification'
  classpath = sourceSets.test.runtimeClasspath

  workingDir 'heron_wsgi/admin_lib'
  main = 'org.python.util.jython'
  args "${buildDir}/resources/test/nose/__main__.py", '--with-doctest'
}
check.dependsOn noseTests

task testSqliteJdbc(type: JavaExec) {
  group = 'verification'
  classpath = sourceSets.main.runtimeClasspath

  workingDir 'heron_wsgi/admin_lib'
  main = 'org.python.util.jython'
  args 'jdbc_test.py'
}
check.dependsOn testSqliteJdbc
